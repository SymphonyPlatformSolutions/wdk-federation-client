plugins {
  id 'java'
  id 'io.freefair.lombok' version '8.0.1'
  id 'org.openapi.generator' version '6.6.0'
  id 'de.undercouch.download' version '5.4.0'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
    mavenContent {
      snapshotsOnly()
    }
  }
}

configurations {
  botJar
}

dependencies {
  compileOnly 'org.finos.symphony.wdk:workflow-language:1.6.0-SNAPSHOT'
  compileOnly 'org.finos.symphony.bdk:symphony-bdk-app-spring-boot-starter:2.14.0'
  implementation 'com.auth0:java-jwt:4.4.0'
  implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  botJar 'org.finos.symphony.wdk:workflow-bot-app:1.6.0-SNAPSHOT:boot'
}

tasks.register('downloadFile', Download) {
  src 'https://3306294353-files.gitbook.io/~/files/v0/b/gitbook-x-prod.appspot.com/o/spaces%2FU492yptxY5Gs1jk0rqHF%2Fuploads%2Fgit-blob-78560a6fa9d3e49144c0d0fd1b09e4a711805c1d%2Fapi.yaml?alt=media'
  dest "${buildDir}/spec.yaml"
}

openApiGenerate {
  generatorName = 'java'
  library = 'resttemplate'
  inputSpec = "$buildDir/spec.yaml"
  outputDir = "$buildDir/generated"
  apiPackage = 'com.symphony.devsol.federation.gen'
  invokerPackage = 'com.symphony.devsol.federation.http'
  modelPackage = 'com.symphony.devsol.federation.model'
  generateApiTests = false
  generateModelTests = false
  generateModelDocumentation = false
  generateApiDocumentation = false
  configOptions.putAll([ dateLibrary: 'custom' ])
  typeMappings.putAll([ typeMapping: 'DateTime=Instant' ])
  importMappings.putAll([ importMapping: 'DateTime=Instant' ])
}
sourceSets.main.java.srcDirs += "$buildDir/generated/src/main/java"

tasks.register('botJar', Copy) {
  description "Downloads WDK jar."
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from configurations.botJar
  into project.projectDir
  rename { f -> "workflow-bot-app.jar" }
}

tasks.register('customActivityLibs', Copy) {
  description "Copies custom activity dependencies in lib folder."
  from jar
  from configurations.runtimeClasspath
  into "lib"
}

tasks.openApiGenerate.dependsOn tasks.downloadFile
tasks.compileJava.dependsOn tasks.openApiGenerate
tasks.build.dependsOn tasks.customActivityLibs
