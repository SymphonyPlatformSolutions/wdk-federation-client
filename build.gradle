plugins {
  id 'java'
  id 'io.freefair.lombok' version '8.0.1'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

repositories {
  mavenCentral()
}

configurations {
  botJar
}

dependencies {
  compileOnly 'org.finos.symphony.wdk:workflow-language:1.5.1'
  compileOnly 'org.finos.symphony.bdk:symphony-bdk-app-spring-boot-starter:2.13.0'
  compileOnly 'org.springframework.retry:spring-retry:1.3.4'
  implementation 'com.auth0:java-jwt:4.4.0'
  botJar 'org.finos.symphony.wdk:workflow-bot-app:1.5.1:boot'
}

tasks.register('botJar', Copy) {
  description "Downloads WDK jar."
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from configurations.botJar
  into project.projectDir
  rename { f -> "workflow-bot-app.jar" }
}

tasks.register('customActivityLibs', Copy) {
  description "Copies custom activity dependencies in lib folder."
  from jar
  from configurations.runtimeClasspath
  into "lib"
}

tasks.build.dependsOn tasks.customActivityLibs
